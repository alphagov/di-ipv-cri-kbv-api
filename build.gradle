plugins {
	id 'idea'
	id 'application'
	id 'java'
	id "jacoco"
	id "com.diffplug.spotless" version "6.0.4"
	id "io.swagger.core.v3.swagger-gradle-plugin" version "2.1.11"
	id 'com.github.bjornvester.wsdl2java' version '1.2'
	id "org.sonarqube" version "3.3"
	id "io.freefair.aspectj.post-compile-weaving" version "6.3.0"
}

group "uk.gov.di.cri.experian.kbv.api"

repositories {
	mavenCentral()
}

sourceCompatibility = 11

dependencies {
	implementation 'org.apache.cxf:cxf-rt-frontend-jaxws:3.4.5',
			'org.apache.cxf:cxf-rt-transports-http:3.4.5',
			'javax.annotation:javax.annotation-api:1.3.2',
			'org.slf4j:slf4j-api:1.7.32',
			'org.slf4j:slf4j-simple:1.7.32',
			'com.sun.xml.messaging.saaj:saaj-impl:1.5.3',
			"com.amazonaws:aws-lambda-java-core:1.2.1",
			"com.amazonaws:aws-lambda-java-events:3.11.0",
			"com.fasterxml.jackson.core:jackson-databind:2.13.0",
			"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.0",
			"org.hibernate.validator:hibernate-validator:6.2.1.Final",
			"org.hibernate:hibernate-validator-annotation-processor:6.2.1.Final",
			"javax.validation:validation-api:2.0.1.Final",
			"javax.el:javax.el-api:3.0.0",
			"org.glassfish:javax.el:3.0.0"

	aspect "software.amazon.lambda:powertools-tracing:1.10.2"

	testImplementation "org.junit.jupiter:junit-jupiter:5.8.2",
			"org.mockito:mockito-junit-jupiter:4.1.0",
			"org.mockito:mockito-inline:4.2.0"
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
	}
}

spotless {
	java {
		target "src/main/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

run {
	debugOptions {
		enabled = true
		port = 8082
		server = true
		suspend = false
	}
}

wsdl2java {
	includes = [
		'wsdl/iiq-wasp-token.wsdl',
		'wsdl/iiq-service.wsdl'
	]
}

sourceSets.main.java.srcDirs = [
	'src/main/java',
	'src/build/generated/sources/wsdl2java'
]

sonarqube {
	properties {
		property "sonar.projectKey", "alphagov_di-cri-experian-kbv-api"
		property "sonar.organization", "alphagov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

mainClassName = "uk.gov.di.cri.experian.kbv.api.App"
